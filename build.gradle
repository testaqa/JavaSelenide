plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.8.1'
}

repositories {
    mavenCentral()
}

def junitVersion = "5.7.0"
def junitPlatformVersion = "1.7.0"
def selenideVersion = "5.19.0"
def ownerVersion = "1.0.12"
def allureVersion = "2.13.8"
def aspectjweaverVersion = "1.9.6"

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
    testImplementation "com.codeborne:selenide:${selenideVersion}"
    testImplementation "org.aeonbits.owner:owner:${ownerVersion}"
    testImplementation "io.qameta.allure:allure-selenide:${allureVersion}"
    testImplementation "org.aspectj:aspectjweaver:${aspectjweaverVersion}"

    testRuntimeOnly "org.junit.platform:junit-platform-launcher:${junitPlatformVersion}"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    useJUnitPlatform()
    systemProperties += System.properties

    if (System.getProperty("threads") != null) {
        systemProperties += [
                'junit.jupiter.execution.parallel.enabled'                 : true,
                'junit.jupiter.execution.parallel.mode.default'            : 'concurrent',
                'junit.jupiter.execution.parallel.mode.classes.default'    : 'concurrent',
                'junit.jupiter.execution.parallel.config.strategy'         : 'fixed',
                'junit.jupiter.execution.parallel.config.fixed.parallelism': System.getProperty("threads").toInteger()
        ]
    }
}

task web(type: Test) {
    useJUnitPlatform {
        includeTags 'web'
    }
    systemProperty 'platform', 'web'
}

allure {
    version = "${allureVersion}"
    autoconfigure = true
    aspectjweaver = true
    useJUnit5 {
        version = "${allureVersion}"
    }
}